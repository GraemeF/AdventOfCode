# Advent of Code 2022

```elixir
Mix.install(kino: "~> 0.7.0")
ExUnit.start()
```

## Intro

This is an attempt at using Elixir to solve the puzzles in [Advent of Code 2022](https://adventofcode.com/2022). To run the solutions, open with [Livebook](https://livebook.dev/).

<!-- livebook:{"branch_parent_index":0} -->

## Day 1: Calorie Counting

Santa's reindeer typically eat regular reindeer food, but they need a lot of magical energy to deliver presents on Christmas. For that, their favorite snack is a special type of star fruit that only grows deep in the jungle. The Elves have brought you on their annual expedition to the grove where the fruit grows.

To supply enough magical energy, the expedition needs to retrieve a minimum of fifty stars by December 25th. Although the Elves assure you that the grove has plenty of fruit, you decide to grab any fruit you see along the way, just in case.

Collect stars by solving puzzles. Two puzzles will be made available on each day in the Advent calendar; the second puzzle is unlocked when you complete the first. Each puzzle grants one star. Good luck!

The jungle must be too overgrown and difficult to navigate in vehicles or access from the air; the Elves' expedition traditionally goes on foot. As your boats approach land, the Elves begin taking inventory of their supplies. One important consideration is food - in particular, the number of Calories each Elf is carrying (your puzzle input).

The Elves take turns writing down the number of Calories contained by the various meals, snacks, rations, etc. that they've brought with them, one item per line. Each Elf separates their own inventory from the previous Elf's inventory (if any) by a blank line.

For example, suppose the Elves finish writing their items' Calories and end up with the following list:

```elixir
example = "1000
2000
3000

4000

5000
6000

7000
8000
9000

10000"
```

This list represents the Calories of the food carried by five Elves:

* The first Elf is carrying food with 1000, 2000, and 3000 Calories, a total of 6000 Calories.
* The second Elf is carrying one food item with 4000 Calories.
* The third Elf is carrying food with 5000 and 6000 Calories, a total of 11000 Calories.
* The fourth Elf is carrying food with 7000, 8000, and 9000 Calories, a total of 24000 Calories.
* The fifth Elf is carrying one food item with 10000 Calories.

In case the Elves get hungry and need extra snacks, they need to know which Elf to ask: they'd like to know how many Calories are being carried by the Elf carrying the most Calories. In the example above, this is 24000 (carried by the fourth Elf).

```elixir
defmodule Day1Part1 do
  def split_elves(input) do
    input
    |> String.split("\n\n")
  end

  def count_elves_calories(elves) do
    elves
    |> Enum.map(&count_elf_calories/1)
  end

  def count_elf_calories(elf) do
    elf
    |> String.split("\n")
    |> Enum.map(&String.to_integer/1)
    |> Enum.sum()
  end

  def calculate(input) do
    input
    |> split_elves
    |> count_elves_calories
    |> Enum.max()
  end
end
```

```elixir
defmodule ExampleTest do
  use ExUnit.Case

  assert example |> Day1Part1.calculate() == 24000
end
```

Find the Elf carrying the most Calories. How many total Calories is that Elf carrying?

```elixir
input = Kino.Input.textarea("Paste your puzzle input:")
```

```elixir
input |> Kino.Input.read() |> Day1Part1.calculate()
```

### Part Two

By the time you calculate the answer to the Elves' question, they've already realized that the Elf carrying the most Calories of food might eventually run out of snacks.

To avoid this unacceptable situation, the Elves would instead like to know the total Calories carried by the top three Elves carrying the most Calories. That way, even if one of those Elves runs out of snacks, they still have two backups.

In the example above, the top three Elves are the fourth Elf (with 24000 Calories), then the third Elf (with 11000 Calories), then the fifth Elf (with 10000 Calories). The sum of the Calories carried by these three elves is 45000.

Find the top three Elves carrying the most Calories. How many Calories are those Elves carrying in total?

```elixir
defmodule Day1Part2 do
  def calculate(input) do
    input
    |> Day1Part1.split_elves()
    |> Day1Part1.count_elves_calories()
    |> Enum.sort(:desc)
    |> Enum.take(3)
    |> Enum.sum()
  end
end
```

```elixir
defmodule ExampleTest do
  use ExUnit.Case

  assert example |> Day1Part2.calculate() == 45000
end
```

```elixir
input |> Kino.Input.read() |> Day1Part2.calculate()
```

<!-- livebook:{"branch_parent_index":0} -->

## Day 2: Rock Paper Scissors

The Elves begin to set up camp on the beach. To decide whose tent gets to be closest to the snack storage, a giant Rock Paper Scissors tournament is already in progress.

Rock Paper Scissors is a game between two players. Each game contains many rounds; in each round, the players each simultaneously choose one of Rock, Paper, or Scissors using a hand shape. Then, a winner for that round is selected: Rock defeats Scissors, Scissors defeats Paper, and Paper defeats Rock. If both players choose the same shape, the round instead ends in a draw.

Appreciative of your help yesterday, one Elf gives you an encrypted strategy guide (your puzzle input) that they say will be sure to help you win. "The first column is what your opponent is going to play: A for Rock, B for Paper, and C for Scissors. The second column--" Suddenly, the Elf is called away to help with someone's tent.

The second column, you reason, must be what you should play in response: X for Rock, Y for Paper, and Z for Scissors. Winning every time would be suspicious, so the responses must have been carefully chosen.

The winner of the whole tournament is the player with the highest score. Your total score is the sum of your scores for each round. The score for a single round is the score for the shape you selected (1 for Rock, 2 for Paper, and 3 for Scissors) plus the score for the outcome of the round (0 if you lost, 3 if the round was a draw, and 6 if you won).

Since you can't be sure if the Elf is trying to help you or trick you, you should calculate the score you would get if you were to follow the strategy guide.

For example, suppose you were given the following strategy guide:

```elixir
example = "A Y
B X
C Z
"
```

This strategy guide predicts and recommends the following:

* In the first round, your opponent will choose Rock (A), and you should choose Paper (Y). This ends in a win for you with a score of 8 (2 because you chose Paper + 6 because you won).
* In the second round, your opponent will choose Paper (B), and you should choose Rock (X). This ends in a loss for you with a score of 1 (1 + 0).
* The third round is a draw with both players choosing Scissors, giving you a score of 3 + 3 = 6.

In this example, if you were to follow the strategy guide, you would get a total score of 15 (8 + 1 + 6).

```elixir
defmodule Day2Part1 do
  def get_shape("A"), do: :rock
  def get_shape("B"), do: :paper
  def get_shape("C"), do: :scissors
  def get_shape("X"), do: :rock
  def get_shape("Y"), do: :paper
  def get_shape("Z"), do: :scissors

  def get_shape_score(shape) do
    %{
      rock: 1,
      paper: 2,
      scissors: 3
    }[shape]
  end

  @loss 0
  @draw 3
  @win 6

  def get_result({x, x}), do: @draw
  def get_result({:rock, :paper}), do: @win
  def get_result({:paper, :scissors}), do: @win
  def get_result({:scissors, :rock}), do: @win
  def get_result({_, _}), do: @loss

  def get_player_score({_, player}), do: player |> get_shape_score

  def get_round_score(plays) do
    get_result(plays) + get_player_score(plays)
  end

  def calculate(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(fn round ->
      round
      |> String.split(" ")
      |> Enum.map(&get_shape/1)
      |> List.to_tuple()
      |> get_round_score
    end)
    |> Enum.sum()
  end
end
```

```elixir
defmodule ExampleTest do
  use ExUnit.Case

  assert example |> Day2Part1.calculate() == 15
end
```

```elixir
input = Kino.Input.textarea("Paste your puzzle input:")
```

```elixir
input |> Kino.Input.read() |> Day2Part1.calculate()
```

### Part Two

The Elf finishes helping with the tent and sneaks back over to you. "Anyway, the second column says how the round needs to end: X means you need to lose, Y means you need to end the round in a draw, and Z means you need to win. Good luck!"

The total score is still calculated in the same way, but now you need to figure out what shape to choose so the round ends as indicated. The example above now goes like this:

* In the first round, your opponent will choose Rock (A), and you need the round to end in a draw (Y), so you also choose Rock. This gives you a score of 1 + 3 = 4.
* In the second round, your opponent will choose Paper (B), and you choose Rock so you lose (X) with a score of 1 + 0 = 1.
* In the third round, you will defeat your opponent's Scissors with Rock for a score of 1 + 6 = 7.
  Now that you're correctly decrypting the ultra top secret strategy guide, you would get a total score of 12.

```elixir
defmodule Day2Part2 do
  def get_response(:win, :rock), do: :paper
  def get_response(:lose, :rock), do: :scissors
  def get_response(:win, :paper), do: :scissors
  def get_response(:lose, :paper), do: :rock
  def get_response(:win, :scissors), do: :rock
  def get_response(:lose, :scissors), do: :paper
  def get_response(:draw, x), do: x

  def get_desired_outcome("X"), do: :lose
  def get_desired_outcome("Y"), do: :draw
  def get_desired_outcome("Z"), do: :win

  def get_shapes({opponent, desired_outcome}) do
    opponent_shape = opponent |> Day2Part1.get_shape()

    {opponent_shape, desired_outcome |> get_desired_outcome |> get_response(opponent_shape)}
  end

  def get_round_score(round) do
    round
    |> String.split(" ")
    |> List.to_tuple()
    |> get_shapes
    |> Day2Part1.get_round_score()
  end

  def calculate(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&get_round_score/1)
    |> Enum.sum()
  end
end
```

```elixir
defmodule ExampleTest do
  use ExUnit.Case

  assert example |> Day2Part2.calculate() == 12
end
```

Following the Elf's instructions for the second column, what would your total score be if everything goes exactly according to your strategy guide?

```elixir
input |> Kino.Input.read() |> Day2Part2.calculate()
```

<!-- livebook:{"branch_parent_index":0} -->

## Day 3: Rucksack Reorganization

One Elf has the important job of loading all of the rucksacks with supplies for the jungle journey. Unfortunately, that Elf didn't quite follow the packing instructions, and so a few items now need to be rearranged.

Each rucksack has two large compartments. All items of a given type are meant to go into exactly one of the two compartments. The Elf that did the packing failed to follow this rule for exactly one item type per rucksack.

The Elves have made a list of all of the items currently in each rucksack (your puzzle input), but they need your help finding the errors. Every item type is identified by a single lowercase or uppercase letter (that is, `a` and `A` refer to different types of items).

The list of items for each rucksack is given as characters all on a single line. A given rucksack always has the same number of items in each of its two compartments, so the first half of the characters represent items in the first compartment, while the second half of the characters represent items in the second compartment.

For example, suppose you have the following list of contents from six rucksacks:

```elixir
example = "vJrwpWtwJgWrhcsFMMfFFhFp
jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
PmmdzqPrVvPwwTWBwg
wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
ttgJtRGJQctTZtZT
CrZsJsPPZsGzwwsLwLmpwMDw"
```

* The first rucksack contains the items `vJrwpWtwJgWrhcsFMMfFFhFp`, which means its first compartment contains the items `vJrwpWtwJgWr`, while the second compartment contains the items `hcsFMMfFFhFp`. The only item type that appears in both compartments is lowercase `p`.
* The second rucksack's compartments contain `jqHRNqRjqzjGDLGL` and `rsFMfFZSrLrFZsSL`. The only item type that appears in both compartments is uppercase `L`.
* The third rucksack's compartments contain `PmmdzqPrV` and `vPwwTWBwg`; the only common item type is uppercase `P`.
* The fourth rucksack's compartments only share item type `v`.
* The fifth rucksack's compartments only share item type `t`.
* The sixth rucksack's compartments only share item type `s`.

To help prioritize item rearrangement, every item type can be converted to a priority:

Lowercase item types `a` through `z` have priorities 1 through 26.
Uppercase item types `A` through `Z` have priorities 27 through 52.
In the above example, the priority of the item type that appears in both compartments of each rucksack is 16 (`p`), 38 (`L`), 42 (`P`), 22 (`v`), 20 (`t`), and 19 (`s`); the sum of these is *157*.

```elixir
defmodule Day3Part1 do
  def split_rucksacks(rucksacks), do: rucksacks |> String.split("\n")

  def split_compartments(rucksack),
    do: rucksack |> String.split_at(rucksack |> String.length() |> div(2))

  def item_types(contents), do: contents |> String.graphemes() |> MapSet.new()

  def find_common_item_types(containers),
    do:
      containers
      |> Enum.map(&item_types/1)
      |> Enum.reduce(fn more, common -> MapSet.intersection(more, common) end)

  def priority(item) when item > "Z", do: (item |> String.to_charlist() |> hd) - 96
  def priority(item), do: (item |> String.to_charlist() |> hd) - 38

  def calculate(input),
    do:
      input
      |> split_rucksacks
      |> Enum.map(&split_compartments/1)
      |> Enum.map(&Tuple.to_list/1)
      |> Enum.map(&find_common_item_types/1)
      |> Enum.map(&MapSet.to_list/1)
      |> List.flatten()
      |> Enum.map(&priority/1)
      |> Enum.sum()
end
```

```elixir
defmodule ExampleTest do
  use ExUnit.Case
  import Day3Part1

  assert example |> split_rucksacks() |> hd |> split_compartments() ==
           {"vJrwpWtwJgWr", "hcsFMMfFFhFp"}

  assert example
         |> split_rucksacks()
         |> hd
         |> split_compartments()
         |> Tuple.to_list()
         |> find_common_item_types() ==
           MapSet.new(["p"])

  assert priority("a") === 1
  assert priority("z") === 26
  assert priority("A") === 27
  assert priority("Z") === 52

  assert example |> calculate() == 157
end
```

Find the item type that appears in both compartments of each rucksack. What is the sum of the priorities of those item types?

```elixir
input = Kino.Input.textarea("Paste your puzzle input:")
```

```elixir
input |> Kino.Input.read() |> Day3Part1.calculate()
```

### Part Two

<!-- livebook:{"break_markdown":true} -->

As you finish identifying the misplaced items, the Elves come to you with another issue.

For safety, the Elves are divided into groups of three. Every Elf carries a badge that identifies their group. For efficiency, within each group of three Elves, the badge is the only item type carried by all three Elves. That is, if a group's badge is item type `B`, then all three Elves will have item type `B` somewhere in their rucksack, and at most two of the Elves will be carrying any other item type.

The problem is that someone forgot to put this year's updated authenticity sticker on the badges. All of the badges need to be pulled out of the rucksacks so the new authenticity stickers can be attached.

Additionally, nobody wrote down which item type corresponds to each group's badges. The only way to tell which item type is the right one is by finding the one item type that is common between all three Elves in each group.

Every set of three lines in your list corresponds to a single group, but each group can have a different badge item type. So, in the above example, the first group's rucksacks are the first three lines:

```
vJrwpWtwJgWrhcsFMMfFFhFp
jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
PmmdzqPrVvPwwTWBwg
```

And the second group's rucksacks are the next three lines:

```
wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
ttgJtRGJQctTZtZT
CrZsJsPPZsGzwwsLwLmpwMDw
```

In the first group, the only item type that appears in all three rucksacks is lowercase `r`; this must be their badges. In the second group, their badge item type must be `Z`.

Priorities for these items must still be found to organize the sticker attachment efforts: here, they are 18 (`r`) for the first group and 52 (`Z`) for the second group. The sum of these is 70.

```elixir
defmodule Day3Part2 do
  import Day3Part1

  def find_common_item_type(rucksacks),
    do: find_common_item_types(rucksacks) |> MapSet.to_list() |> hd

  def calculate(input),
    do:
      input
      |> split_rucksacks
      |> Enum.chunk_every(3)
      |> Enum.map(&find_common_item_type/1)
      |> Enum.map(&priority/1)
      |> Enum.sum()
end
```

```elixir
defmodule ExampleTest do
  use ExUnit.Case
  import Day3Part1
  import Day3Part2

  assert example |> split_rucksacks() |> Enum.take(3) |> find_common_item_type ==
           "r"

  assert example |> Day3Part2.calculate() == 70
end
```

Find the item type that corresponds to the badges of each three-Elf group. What is the sum of the priorities of those item types?

```elixir
input |> Kino.Input.read() |> Day3Part2.calculate()
```

<!-- livebook:{"branch_parent_index":0} -->

## Day 4: Camp Cleanup

Space needs to be cleared before the last supplies can be unloaded from the ships, and so several Elves have been assigned the job of cleaning up sections of the camp. Every section has a unique ID number, and each Elf is assigned a range of section IDs.

However, as some of the Elves compare their section assignments with each other, they've noticed that many of the assignments overlap. To try to quickly find overlaps and reduce duplicated effort, the Elves pair up and make a big list of the section assignments for each pair (your puzzle input).

For example, consider the following list of section assignment pairs:

```elixir
example = "2-4,6-8
2-3,4-5
5-7,7-9
2-8,3-7
6-6,4-6
2-6,4-8"
```

For the first few pairs, this list means:

* Within the first pair of Elves, the first Elf was assigned sections 2-4 (sections 2, 3, and 4), while the second Elf was assigned sections 6-8 (sections 6, 7, 8).
* The Elves in the second pair were each assigned two sections.
* The Elves in the third pair were each assigned three sections: one got sections 5, 6, and 7, while the other also got 7, plus 8 and 9.
* This example list uses single-digit section IDs to make it easier to draw; your actual list might contain larger numbers. Visually, these pairs of section assignments look like this:

```
.234.....  2-4
.....678.  6-8

.23......  2-3
...45....  4-5

....567..  5-7
......789  7-9

.2345678.  2-8
..34567..  3-7

.....6...  6-6
...456...  4-6

.23456...  2-6
...45678.  4-8
```

<!-- livebook:{"break_markdown":true} -->

Some of the pairs have noticed that one of their assignments fully contains the other. For example, 2-8 fully contains 3-7, and 6-6 is fully contained by 4-6. In pairs where one assignment fully contains the other, one Elf in the pair would be exclusively cleaning sections their partner will already be cleaning, so these seem like the most in need of reconsideration. In this example, there are 2 such pairs.

```elixir
defmodule Day4Part1 do
  def contains?({a, b}, {c, d}), do: a >= c and b <= d

  def contains?([first, second]),
    do: contains?(first, second) or contains?(second, first)

  def parse_range(range),
    do: range |> String.split("-") |> Enum.map(&String.to_integer/1) |> List.to_tuple()

  def parse_pairs(input),
    do:
      input
      |> String.split()
      |> Enum.map(fn pair -> pair |> String.split(",") end)
      |> Enum.map(fn range -> range |> Enum.map(&parse_range/1) end)

  def calculate(input) do
    input
    |> parse_pairs
    |> Enum.filter(&contains?/1)
    |> Enum.count()
  end
end
```

```elixir
defmodule ExampleTest do
  use ExUnit.Case
  import Day4Part1

  assert example |> calculate == 2
end
```

In how many assignment pairs does one range fully contain the other?

```elixir
input = Kino.Input.textarea("Paste your puzzle input:")
```

```elixir
input |> Kino.Input.read() |> Day4Part1.calculate()
```

### Part Two

<!-- livebook:{"break_markdown":true} -->

It seems like there is still quite a bit of duplicate work planned. Instead, the Elves would like to know the number of pairs that overlap at all.

In the above example, the first two pairs (2-4,6-8 and 2-3,4-5) don't overlap, while the remaining four pairs (5-7,7-9, 2-8,3-7, 6-6,4-6, and 2-6,4-8) do overlap:

* 5-7,7-9 overlaps in a single section, 7.
* 2-8,3-7 overlaps all of the sections 3 through 7.
* 6-6,4-6 overlaps in a single section, 6.
* 2-6,4-8 overlaps in sections 4, 5, and 6.

So, in this example, the number of overlapping assignment pairs is 4.

```elixir
defmodule Day4Part2 do
  import Day4Part1

  def within?(a, {c, d}), do: a >= c and a <= d
  def overlaps?({a, b}, c), do: within?(a, c) or within?(b, c)
  def overlaps?([a, b]), do: overlaps?(a, b) or overlaps?(b, a)

  def calculate(input) do
    input
    |> parse_pairs
    |> Enum.filter(&overlaps?/1)
    |> Enum.count()
  end
end
```

```elixir
defmodule ExampleTest do
  use ExUnit.Case
  import Day4Part2

  assert example |> calculate == 4
end
```

In how many assignment pairs do the ranges overlap?

```elixir
input |> Kino.Input.read() |> Day4Part2.calculate()
```

<!-- livebook:{"branch_parent_index":0} -->

## Day 5: Supply Stacks

The expedition can depart as soon as the final supplies have been unloaded from the ships. Supplies are stored in stacks of marked crates, but because the needed supplies are buried under many other crates, the crates need to be rearranged.

The ship has a giant cargo crane capable of moving crates between stacks. To ensure none of the crates get crushed or fall over, the crane operator will rearrange them in a series of carefully-planned steps. After the crates are rearranged, the desired crates will be at the top of each stack.

The Elves don't want to interrupt the crane operator during this delicate procedure, but they forgot to ask her which crate will end up where, and they want to be ready to unload them as soon as possible so they can embark.

They do, however, have a drawing of the starting stacks of crates and the rearrangement procedure (your puzzle input). For example:

```elixir
example = "    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 

move 1 from 2 to 1
move 3 from 1 to 3
move 2 from 2 to 1
move 1 from 1 to 2"
```

In this example, there are three stacks of crates. Stack 1 contains two crates: crate Z is on the bottom, and crate N is on top. Stack 2 contains three crates; from bottom to top, they are crates M, C, and D. Finally, stack 3 contains a single crate, P.

<!-- livebook:{"break_markdown":true} -->

Then, the rearrangement procedure is given. In each step of the procedure, a quantity of crates is moved from one stack to a different stack. In the first step of the above rearrangement procedure, one crate is moved from stack 2 to stack 1, resulting in this configuration:

```
[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

In the second step, three crates are moved from stack 1 to stack 3. Crates are moved one at a time, so the first crate to be moved (D) ends up below the second and third crates:

```
        [Z]
        [N]
    [C] [D]
    [M] [P]
 1   2   3
```

Then, both crates are moved from stack 2 to stack 1. Again, because crates are moved one at a time, crate C ends up below crate M:

```
        [Z]
        [N]
[M]     [D]
[C]     [P]
 1   2   3
```

Finally, one crate is moved from stack 1 to stack 2:

```
        [Z]
        [N]
        [D]
[C] [M] [P]
 1   2   3
```

The Elves just need to know which crate will end up on top of each stack; in this example, the top crates are C in stack 1, M in stack 2, and Z in stack 3, so you should combine these together and give the Elves the message CMZ.

```elixir
defmodule Day5Part1 do
  def split_row(""), do: []

  def split_row(input) do
    {first, rest} = input |> String.split_at(4)
    [first] ++ split_row(rest)
  end

  def parse_cell(""), do: nil
  def parse_cell("[" <> <<crate::bytes-size(1)>> <> "]"), do: crate
  def parse_cell(stack_label), do: stack_label

  def parse_row(input) do
    input
    |> split_row
    |> Enum.map(&String.trim/1)
    |> Enum.map(&parse_cell/1)
  end

  def get_stack(input) do
    [label | crates] = input |> Tuple.to_list() |> Enum.filter(fn x -> x != nil end)
    %{label => crates |> Enum.reverse()}
  end

  def parse_stacks(input) do
    input
    |> String.split("\n")
    |> Enum.map(&parse_row/1)
    |> Enum.reverse()
    |> Enum.zip()
    |> Enum.map(&get_stack/1)
    |> Enum.reduce(%{}, fn x, a -> Map.merge(x, a) end)
  end

  def parse_step(input) do
    %{"quantity" => quantity, "from" => from, "to" => to} =
      Regex.named_captures(~r/move (?<quantity>\d+) from (?<from>\d+) to (?<to>\d+)/, input)

    %{quantity: quantity |> String.to_integer(), from: from, to: to}
  end

  def parse_procedure(input) do
    input
    |> String.split("\n")
    |> Enum.map(&parse_step/1)
  end

  def parse_input(input) do
    [stacks_input, procedure_input] = input |> String.split("\n\n")
    {stacks_input |> parse_stacks, procedure_input |> parse_procedure}
  end

  def rearrange(%{quantity: 0}, stacks, _), do: stacks

  def cratemover_9000(stacks, from, _quantity) do
    [taken | remaining] = Map.get(stacks, from)
    {[taken], remaining}
  end

  def rearrange(%{quantity: quantity, from: from, to: to}, stacks, pickup) when quantity > 0 do
    {taken, remaining} = pickup.(stacks, from, quantity)

    %{quantity: quantity - (taken |> Enum.count()), from: from, to: to}
    |> rearrange(%{stacks | from => remaining, to => taken ++ Map.get(stacks, to)}, pickup)
  end

  def top_crates(stacks) do
    stacks |> Enum.map(fn {_, stack} -> stack |> hd end)
  end

  def to_top_crates_string(stacks) do
    stacks
    |> Map.to_list()
    |> Enum.sort_by(fn {label, _} -> label end)
    |> top_crates
    |> Enum.reverse()
    |> Enum.reduce(&<>/2)
  end

  def calculate(input) do
    {stacks, procedure} = input |> parse_input

    procedure
    |> Enum.reduce(stacks, fn step, stacks ->
      rearrange(step, stacks, &Day5Part1.cratemover_9000/3)
    end)
    |> to_top_crates_string
  end
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule ExampleTest do
  use ExUnit.Case
  import Day5Part1

  assert example |> parse_input |> elem(0) == %{
           "1" => ["N", "Z"],
           "2" => ["D", "C", "M"],
           "3" => ["P"]
         }

  assert example |> parse_input |> elem(1) |> hd == %{quantity: 1, from: "2", to: "1"}

  assert example |> calculate == "CMZ"
end
```

After the rearrangement procedure completes, what crate ends up on top of each stack?

```elixir
input = Kino.Input.textarea("Paste your puzzle input:")
```

```elixir
input |> Kino.Input.read() |> Day5Part1.calculate()
```

### Part Two

As you watch the crane operator expertly rearrange the crates, you notice the process isn't following your prediction.

Some mud was covering the writing on the side of the crane, and you quickly wipe it away. The crane isn't a CrateMover 9000 - it's a CrateMover 9001.

The CrateMover 9001 is notable for many new and exciting features: air conditioning, leather seats, an extra cup holder, and the ability to pick up and move multiple crates at once.

Again considering the example above, the crates begin in the same configuration:

```
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

Moving a single crate from stack 2 to stack 1 behaves the same as before:

```
[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

However, the action of moving three crates from stack 1 to stack 3 means that those three moved crates stay in the same order, resulting in this new configuration:

```
        [D]
        [N]
    [C] [Z]
    [M] [P]
 1   2   3
```

Next, as both crates are moved from stack 2 to stack 1, they retain their order as well:

```
        [D]
        [N]
[C]     [Z]
[M]     [P]
 1   2   3
```

Finally, a single crate is still moved from stack 1 to stack 2, but now it's crate C that gets moved:

```
        [D]
        [N]
        [Z]
[M] [C] [P]
 1   2   3
```

In this example, the CrateMover 9001 has put the crates in a totally different order: MCD.

```elixir
defmodule Day5Part2 do
  import Day5Part1

  def cratemover_9001(stacks, from, quantity), do: Map.get(stacks, from) |> Enum.split(quantity)

  def calculate(input) do
    {stacks, procedure} = input |> parse_input

    procedure
    |> Enum.reduce(stacks, fn step, stacks ->
      rearrange(step, stacks, &cratemover_9001/3)
    end)
    |> to_top_crates_string
  end
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule ExampleTestPart2 do
  use ExUnit.Case
  import Day5Part2

  assert example |> calculate == "MCD"
end
```

Before the rearrangement process finishes, update your simulation so that the Elves know where they should stand to be ready to unload the final supplies. After the rearrangement procedure completes, what crate ends up on top of each stack?

```elixir
input |> Kino.Input.read() |> Day5Part2.calculate()
```

<!-- livebook:{"branch_parent_index":0} -->

## Day 6: Tuning Trouble

The preparations are finally complete; you and the Elves leave camp on foot and begin to make your way toward the star fruit grove.

As you move through the dense undergrowth, one of the Elves gives you a handheld device. He says that it has many fancy features, but the most important one to set up right now is the communication system.

However, because he's heard you have significant experience dealing with signal-based systems, he convinced the other Elves that it would be okay to give you their one malfunctioning device - surely you'll have no problem fixing it.

As if inspired by comedic timing, the device emits a few colorful sparks.

To be able to communicate with the Elves, the device needs to lock on to their signal. The signal is a series of seemingly-random characters that the device receives one at a time.

To fix the communication system, you need to add a subroutine to the device that detects a start-of-packet marker in the datastream. In the protocol being used by the Elves, the start of a packet is indicated by a sequence of four characters that are all different.

The device will send your subroutine a datastream buffer (your puzzle input); your subroutine needs to identify the first position where the four most recently received characters were all different. Specifically, it needs to report the number of characters from the beginning of the buffer to the end of the first such four-character marker.

For example, suppose you receive the following datastream buffer:

```elixir
example = "mjqjpqmgbljsphdztnvjfqwrcgsmlb"
```

After the first three characters (mjq) have been received, there haven't been enough characters received yet to find the marker. The first time a marker could occur is after the fourth character is received, making the most recent four characters mjqj. Because j is repeated, this isn't a marker.

The first time a marker appears is after the seventh character arrives. Once it does, the last four characters received are jpqm, which are all different. In this case, your subroutine should report the value 7, because the first start-of-packet marker is complete after 7 characters have been processed.

```elixir
defmodule Day6Part1 do
  def calculate(input, marker_length),
    do:
      input
      |> String.graphemes()
      |> Enum.chunk_every(marker_length, 1)
      |> Enum.find_index(fn chunk ->
        chunk |> Enum.uniq() |> Enum.count() == marker_length
      end)
      |> Kernel.+(marker_length)

  def calculate(input), do: input |> calculate(4)
end
```

Here are a few more examples:

```elixir
defmodule ExampleTest do
  use ExUnit.Case
  import Day6Part1

  assert example |> calculate == 7
  assert "bvwbjplbgvbhsrlpgdmjqwftvncz" |> calculate == 5
  assert "nppdvjthqldpwncqszvftbrmjlhg" |> calculate == 6
  assert "nznrnfrfntjfmvfwmzdfjlvtqnbhcprsg" |> calculate == 10
  assert "zcfzfwzzqfrljwzlrfnpqdbhtmscgvjw" |> calculate == 11
end
```

How many characters need to be processed before the first start-of-packet marker is detected?

```elixir
input = Kino.Input.textarea("Paste your puzzle input:")
```

```elixir
input |> Kino.Input.read() |> Day6Part1.calculate(4)
```

### Part Two

Your device's communication system is correctly detecting packets, but still isn't working. It looks like it also needs to look for messages.

A start-of-message marker is just like a start-of-packet marker, except it consists of 14 distinct characters rather than 4.

```elixir
defmodule Day6Part2 do
  import Day6Part1

  def calculate(input), do: input |> calculate(14)
end
```

Here are the first positions of start-of-message markers for all of the above examples:

```elixir
defmodule ExampleTest do
  use ExUnit.Case
  import Day6Part2

  assert example |> calculate == 19
  assert "bvwbjplbgvbhsrlpgdmjqwftvncz" |> calculate == 23
  assert "nppdvjthqldpwncqszvftbrmjlhg" |> calculate == 23
  assert "nznrnfrfntjfmvfwmzdfjlvtqnbhcprsg" |> calculate == 29
  assert "zcfzfwzzqfrljwzlrfnpqdbhtmscgvjw" |> calculate == 26
end
```

How many characters need to be processed before the first start-of-message marker is detected?

```elixir
input |> Kino.Input.read() |> Day6Part1.calculate(14)
```

<!-- livebook:{"branch_parent_index":0} -->

## Day 7: No Space Left On Device

You can hear birds chirping and raindrops hitting leaves as the expedition proceeds. Occasionally, you can even hear much louder sounds in the distance; how big do the animals get out here, anyway?

The device the Elves gave you has problems with more than just its communication system. You try to run a system update:

```
$ system-update --please --pretty-please-with-sugar-on-top
Error: No space left on device
```

Perhaps you can delete some files to make space for the update?

You browse around the filesystem to assess the situation and save the resulting terminal output (your puzzle input). For example:

```elixir
example = "$ cd /
$ ls
dir a
14848514 b.txt
8504156 c.dat
dir d
$ cd a
$ ls
dir e
29116 f
2557 g
62596 h.lst
$ cd e
$ ls
584 i
$ cd ..
$ cd ..
$ cd d
$ ls
4060174 j
8033020 d.log
5626152 d.ext
7214296 k"
```

The filesystem consists of a tree of files (plain data) and directories (which can contain other directories or files). The outermost directory is called /. You can navigate around the filesystem, moving into or out of directories and listing the contents of the directory you're currently in.

Within the terminal output, lines that begin with $ are commands you executed, very much like some modern computers:

* cd means change directory. This changes which directory is the current directory, but the specific result depends on the argument:
  * `cd x` moves in one level: it looks in the current directory for the directory named x and makes it the current directory.
  * `cd ..` moves out one level: it finds the directory that contains the current directory, then makes that directory the current directory.
  * `cd /` switches the current directory to the outermost directory, /.
* ls means list. It prints out all of the files and directories immediately contained by the current directory:
  * `123 abc` means that the current directory contains a file named abc with size 123.
  * `dir xyz` means that the current directory contains a directory named xyz.

Given the commands and output in the example above, you can determine that the filesystem looks visually like this:

```
- / (dir)
  - a (dir)
    - e (dir)
      - i (file, size=584)
    - f (file, size=29116)
    - g (file, size=2557)
    - h.lst (file, size=62596)
  - b.txt (file, size=14848514)
  - c.dat (file, size=8504156)
  - d (dir)
    - j (file, size=4060174)
    - d.log (file, size=8033020)
    - d.ext (file, size=5626152)
    - k (file, size=7214296)
```

Here, there are four directories: / (the outermost directory), a and d (which are in /), and e (which is in a). These directories also contain files of various sizes.

Since the disk is full, your first step should probably be to find directories that are good candidates for deletion. To do this, you need to determine the total size of each directory. The total size of a directory is the sum of the sizes of the files it contains, directly or indirectly. (Directories themselves do not count as having any intrinsic size.)

The total sizes of the directories above can be found as follows:

* The total size of directory e is 584 because it contains a single file i of size 584 and no other directories.
* The directory a has total size 94853 because it contains files f (size 29116), g (size 2557), and h.lst (size 62596), plus file i indirectly (a contains e which contains i).
* Directory d has total size 24933642.
* As the outermost directory, / contains every file. Its total size is 48381165, the sum of the size of every file.

To begin, find all of the directories with a total size of at most 100000, then calculate the sum of their total sizes. In the example above, these directories are a and e; the sum of their total sizes is 95437 (94853 + 584). (As in this example, this process can count files more than once!)

```elixir
defmodule Day7Part1 do
  def is_file?({:file, _, _}), do: true
  def is_file?(_), do: false

  def parse_content("dir " <> name), do: {:dir, name}

  def parse_content(input) do
    [size, name] = input |> String.split(" ")
    {:file, name, size |> String.to_integer()}
  end

  def parse_command("cd " <> path), do: {:cd, path}

  def parse_command("ls\n" <> contents),
    do: {:ls, contents |> String.split("\n") |> Enum.map(&parse_content/1)}

  def parse(input),
    do:
      input
      |> String.split("$", trim: true)
      |> Enum.map(&String.trim/1)
      |> Enum.map(&parse_command/1)

  def process({:cd, "/"}, {_, files}) do
    {[], files}
  end

  def process({:cd, ".."}, {cwd, files}) do
    {cwd |> Enum.drop(-1), files}
  end

  def process({:cd, name}, {cwd, files}) do
    {cwd ++ [name], files}
  end

  def process({:ls, contents}, {cwd, files}) do
    {cwd, Map.merge(files, contents |> filter_contents(cwd))}
  end

  def filter_contents(contents, cwd),
    do:
      contents
      |> Enum.filter(&is_file?/1)
      |> Enum.map(fn {:file, name, size} -> {cwd ++ [name], size} end)
      |> Enum.into(%{})

  def build_tree(commands) do
    {_, files} =
      commands
      |> Enum.reduce({nil, %{}}, fn e, a -> process(e, a) end)

    files
  end

  def directories(file_path) do
    0..((file_path |> Enum.count()) - 1)
    |> Enum.map(fn count -> file_path |> Enum.take(count) end)
  end

  def size_by_directory(tree) do
    tree
    |> Enum.reduce(%{}, fn {path, size}, dirs ->
      Map.merge(
        dirs,
        path
        |> directories
        |> Enum.map(fn dir -> {dir, size} end)
        |> Enum.into(%{}),
        fn dir, a, b ->
          {dir, a, b}
          a + b
        end
      )
    end)
  end

  def get_directory_size(input, path),
    do:
      input
      |> parse
      |> build_tree
      |> size_by_directory
      |> Map.get(path)

  def calculate(input),
    do:
      input
      |> parse
      |> build_tree
      |> size_by_directory
      |> Map.filter(fn {_, size} -> size <= 100_000 end)
      |> Enum.map(fn {_, size} -> size end)
      |> Enum.sum()
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule ExampleTest do
  use ExUnit.Case
  import Day7Part1

  assert example |> parse |> Enum.take(2) == [
           {:cd, "/"},
           {:ls,
            [{:dir, "a"}, {:file, "b.txt", 14_848_514}, {:file, "c.dat", 8_504_156}, {:dir, "d"}]}
         ]

  assert example |> get_directory_size(["a", "e"]) == 584
  assert example |> get_directory_size(["a"]) == 94853
  assert example |> get_directory_size(["d"]) == 24_933_642

  assert example |> calculate == 95437
end
```

Find all of the directories with a total size of at most 100000. What is the sum of the total sizes of those directories?

```elixir
input = Kino.Input.textarea("Paste your puzzle input:")
```

```elixir
input |> Kino.Input.read() |> Day7Part1.calculate()
```

### Part Two

Now, you're ready to choose a directory to delete.

The total disk space available to the filesystem is 70000000. To run the update, you need unused space of at least 30000000. You need to find a directory you can delete that will free up enough space to run the update.

In the example above, the total size of the outermost directory (and thus the total amount of used space) is 48381165; this means that the size of the unused space must currently be 21618835, which isn't quite the 30000000 required by the update. Therefore, the update still requires a directory with total size of at least 8381165 to be deleted before it can run.

To achieve this, you have the following options:

* Delete directory e, which would increase unused space by 584.
* Delete directory a, which would increase unused space by 94853.
* Delete directory d, which would increase unused space by 24933642.
* Delete directory /, which would increase unused space by 48381165.

Directories e and a are both too small; deleting them would not free up enough space. However, 
directories d and / are both big enough! Between these, choose the smallest: d, increasing unused space by 24933642.

Find the smallest directory that, if deleted, would free up enough space on the filesystem to run the update. What is the total size of that directory?

```elixir
defmodule Day7Part2 do
  import Day7Part1

  @total_required_space 30_000_000
  @total_disk_size 70_000_000

  def calculate(input) do
    directories =
      input
      |> parse
      |> build_tree
      |> size_by_directory

    space = @total_disk_size - directories[[]]
    additional_space_required = (@total_required_space - space) |> IO.inspect()

    input
    |> parse
    |> build_tree
    |> size_by_directory
    |> IO.inspect()
    |> Map.values()
    |> Enum.filter(fn size -> size >= additional_space_required end)
    |> Enum.sort()
    |> hd
  end
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule ExampleTest do
  use ExUnit.Case
  import Day7Part2

  assert example |> calculate == 24_933_642
end
```

```elixir
input |> Kino.Input.read() |> Day7Part2.calculate()
```

<!-- livebook:{"branch_parent_index":0} -->

## Day 8: Treetop Tree House

The expedition comes across a peculiar patch of tall trees all planted carefully in a grid. The Elves explain that a previous expedition planted these trees as a reforestation effort. Now, they're curious if this would be a good location for a tree house.

First, determine whether there is enough tree cover here to keep a tree house hidden. To do this, you need to count the number of trees that are visible from outside the grid when looking directly along a row or column.

The Elves have already launched a quadcopter to generate a map with the height of each tree (your puzzle input). For example:

```elixir
example = "30373
25512
65332
33549
35390"
```

Each tree is represented as a single digit whose value is its height, where 0 is the shortest and 9 is the tallest.

A tree is visible if all of the other trees between it and an edge of the grid are shorter than it. Only consider trees in the same row or column; that is, only look up, down, left, or right from any given tree.

All of the trees around the edge of the grid are visible - since they are already on the edge, there are no trees to block the view. In this example, that only leaves the interior nine trees to consider:

* The top-left 5 is visible from the left and top. (It isn't visible from the right or bottom since other trees of height 5 are in the way.)
* The top-middle 5 is visible from the top and right.
* The top-right 1 is not visible from any direction; for it to be visible, there would need to only be trees of height 0 between it and an edge.
* The left-middle 5 is visible, but only from the right.
* The center 3 is not visible from any direction; for it to be visible, there would need to be only trees of at most height 2 between it and an edge.
* The right-middle 3 is visible from the right.
* In the bottom row, the middle 5 is visible, but the 3 and 4 are not.

With 16 trees visible on the edge and another 5 visible in the interior, a total of 21 trees are visible in this arrangement.

```elixir
defmodule Day8Part1 do
  @max_height 9

  def move({x, y}, {dx, dy}), do: {x + dx, y + dy}

  def visible_trees(
        trees,
        tree_location,
        visible_height,
        direction
      ) do
    tree_height = trees |> Map.get(tree_location)

    cond do
      tree_height == nil ->
        %{}

      visible_height > @max_height ->
        %{}

      tree_height >= visible_height ->
        visible_trees(trees, tree_location |> move(direction), tree_height + 1, direction)
        |> Map.put(tree_location, tree_height)

      tree_height < visible_height ->
        visible_trees(trees, tree_location |> move(direction), visible_height, direction)
    end
  end

  def column_walks({columns, rows}),
    do:
      0..(columns - 1)
      |> Enum.map(fn column -> [{{column, 0}, {0, 1}}, {{column, rows - 1}, {0, -1}}] end)

  def row_walks({columns, rows}),
    do:
      1..(rows - 1)
      |> Enum.map(fn row -> [{{0, row}, {1, 0}}, {{columns - 1, row}, {-1, 0}}] end)

  def visible_trees({trees, size}),
    do:
      ((size
        |> row_walks) ++ (size |> column_walks))
      |> List.flatten()
      |> Enum.reduce(%{}, fn {start_tree, direction}, found_trees ->
        visible_trees(trees, start_tree, 0, direction) |> Map.merge(found_trees)
      end)

  def parse({input, row_index}),
    do:
      input
      |> String.graphemes()
      |> Enum.with_index()
      |> Enum.map(fn {tree, column_index} ->
        %{{row_index, column_index} => tree |> String.to_integer()}
      end)

  def parse(input) do
    input_rows =
      input
      |> String.split("\n")

    {input_rows
     |> Enum.with_index()
     |> Enum.map(&parse/1)
     |> List.flatten()
     |> Enum.reduce(%{}, &Map.merge/2),
     {input_rows |> hd |> String.length(), input_rows |> Enum.count()}}
  end

  def calculate(input),
    do:
      input
      |> parse
      |> visible_trees
      |> Enum.count()
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule ExampleTest do
  use ExUnit.Case
  import Day8Part1

  assert example |> calculate == 21
end
```

Consider your map; how many trees are visible from outside the grid?

```elixir
input = Kino.Input.textarea("Paste your puzzle input:")
```

```elixir
input |> Kino.Input.read() |> Day8Part1.calculate()
```

### Part Two

Content with the amount of tree cover available, the Elves just need to know the best spot to build their tree house: they would like to be able to see a lot of trees.

To measure the viewing distance from a given tree, look up, down, left, and right from that tree; stop if you reach an edge or at the first tree that is the same height or taller than the tree under consideration. (If a tree is right on the edge, at least one of its viewing distances will be zero.)

The Elves don't care about distant trees taller than those found by the rules above; the proposed tree house has large eaves to keep it dry, so they wouldn't be able to see higher than the tree house anyway.

In the example above, consider the middle 5 in the second row:

```
30373
25512
65332
33549
35390
```

* Looking up, its view is not blocked; it can see 1 tree (of height 3).
* Looking left, its view is blocked immediately; it can see only 1 tree (of height 5, right next to it).
* Looking right, its view is not blocked; it can see 2 trees.
* Looking down, its view is blocked eventually; it can see 2 trees (one of height 3, then the tree of height 5 that blocks its view).

A tree's scenic score is found by multiplying together its viewing distance in each of the four directions. For this tree, this is 4 (found by multiplying 1 * 1 * 2 * 2).

```elixir
defmodule Day8Part2 do
  import Day8Part1

  def visible_trees(
        {{_, _} = tree_location, direction},
        trees,
        stop_height
      ) do
    tree_height =
      trees
      |> Map.get(tree_location)

    cond do
      tree_height == nil ->
        0

      tree_height < stop_height ->
        1 +
          visible_trees({tree_location |> move(direction), direction}, trees, stop_height)

      tree_height >= stop_height ->
        1
    end
  end

  def tree_walks(tree_location) do
    [
      {tree_location, {1, 0}},
      {tree_location, {-1, 0}},
      {tree_location, {0, 1}},
      {tree_location, {0, -1}}
    ]
    |> Enum.map(fn {location, direction} -> {move(location, direction), direction} end)
  end

  def scenic_score({location, height}, trees) do
    location
    |> tree_walks
    |> Enum.map(fn walk ->
      walk
      |> visible_trees(trees, height)
    end)
    |> Enum.reduce(1, fn tree_count, score -> score * tree_count end)
  end

  def calculate(input) do
    {trees, _} = input |> parse

    trees |> Enum.map(fn tree -> tree |> scenic_score(trees) end) |> Enum.max()
  end
end
```

However, you can do even better: consider the tree of height 5 in the middle of the fourth row:

```
30373
25512
65332
33549
35390
```

* Looking up, its view is blocked at 2 trees (by another tree with a height of 5).
* Looking left, its view is not blocked; it can see 2 trees.
* Looking down, its view is also not blocked; it can see 1 tree.
* Looking right, its view is blocked at 2 trees (by a massive tree of height 9).

This tree's scenic score is 8 (2 * 2 * 1 * 2); this is the ideal spot for the tree house.

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule Helpers do
  import Day8Part1
  import Day8Part2

  def tree_score(location, example) do
    {trees, _} = example |> parse
    tree = {location, trees |> Map.get(location)}
    tree |> scenic_score(trees)
  end
end

defmodule ExampleTest do
  use ExUnit.Case
  import Day8Part2

  assert example |> calculate == 8
end
```

```elixir
input |> Kino.Input.read() |> Day8Part2.calculate()
```

<!-- livebook:{"branch_parent_index":0} -->

## Day 9: Rope Bridge

This rope bridge creaks as you walk along it. You aren't sure how old it is, or whether it can even support your weight.

It seems to support the Elves just fine, though. The bridge spans a gorge which was carved out by the massive river far below you.

You step carefully; as you do, the ropes stretch and twist. You decide to distract yourself by modeling rope physics; maybe you can even figure out where not to step.

Consider a rope with a knot at each end; these knots mark the head and the tail of the rope. If the head moves far enough away from the tail, the tail is pulled toward the head.

Due to nebulous reasoning involving Planck lengths, you should be able to model the positions of the knots on a two-dimensional grid. Then, by following a hypothetical series of motions (your puzzle input) for the head, you can determine how the tail will move.

Due to the aforementioned Planck lengths, the rope must be quite short; in fact, the head (H) and tail (T) must always be touching (diagonally adjacent and even overlapping both count as touching):

```
....
.TH.
....

....
.H..
..T.
....

...
.H. (H covers T)
...
```

If the head is ever two steps directly up, down, left, or right from the tail, the tail must also move one step in that direction so it remains close enough:

```
.....    .....    .....
.TH.. -> .T.H. -> ..TH.
.....    .....    .....

...    ...    ...
.T.    .T.    ...
.H. -> ... -> .T.
...    .H.    .H.
...    ...    ...
```

Otherwise, if the head and tail aren't touching and aren't in the same row or column, the tail always moves one step diagonally to keep up:

```
.....    .....    .....
.....    ..H..    ..H..
..H.. -> ..... -> ..T..
.T...    .T...    .....
.....    .....    .....

.....    .....    .....
.....    .....    .....
..H.. -> ...H. -> ..TH.
.T...    .T...    .....
.....    .....    .....
```

You just need to work out where the tail goes as the head follows a series of motions. Assume the head and the tail both start at the same position, overlapping.

For example:

```elixir
example = "R 4
U 4
L 3
D 1
R 4
D 1
L 5
R 2"
```

This series of motions moves the head right four steps, then up four steps, then left three steps, then down one step, and so on. After each step, you'll need to update the position of the tail if the step means the head is no longer adjacent to the tail. Visually, these motions occur as follows (s marks the starting position as a reference point):

```
== Initial State ==

......
......
......
......
H.....  (H covers T, s)

== R 4 ==

......
......
......
......
TH....  (T covers s)

......
......
......
......
sTH...

......
......
......
......
s.TH..

......
......
......
......
s..TH.

== U 4 ==

......
......
......
....H.
s..T..

......
......
....H.
....T.
s.....

......
....H.
....T.
......
s.....

....H.
....T.
......
......
s.....

== L 3 ==

...H..
....T.
......
......
s.....

..HT..
......
......
......
s.....

.HT...
......
......
......
s.....

== D 1 ==

..T...
.H....
......
......
s.....

== R 4 ==

..T...
..H...
......
......
s.....

..T...
...H..
......
......
s.....

......
...TH.
......
......
s.....

......
....TH
......
......
s.....

== D 1 ==

......
....T.
.....H
......
s.....

== L 5 ==

......
....T.
....H.
......
s.....

......
....T.
...H..
......
s.....

......
......
..HT..
......
s.....

......
......
.HT...
......
s.....

......
......
HT....
......
s.....

== R 2 ==

......
......
.H....  (H covers T)
......
s.....

......
......
.TH...
......
s.....
```

After simulating the rope, you can count up all of the positions the tail visited at least once. In this diagram, s again marks the starting position (which the tail also visited) and # marks other positions the tail visited:

```
..##..
...##.
.####.
....#.
s###..
```

So, there are 13 positions the tail visited at least once.

```elixir
defmodule Day9Part1 do
  def vector("R"), do: {1, 0}
  def vector("U"), do: {0, -1}
  def vector("L"), do: {-1, 0}
  def vector("D"), do: {0, 1}

  def expand({direction, steps}), do: 1..steps |> Enum.map(fn _ -> direction |> vector end)

  def parse(input),
    do:
      input
      |> String.split("\n")
      |> Enum.map(fn line ->
        line
        |> String.split(" ")
        |> Kernel.then(fn [direction, steps] -> {direction, steps |> String.to_integer()} end)
      end)
      |> Enum.flat_map(&expand/1)

  def move_end({dx, dy}, {x, y}), do: {x + dx, y + dy}

  def move_tail({hx, hy}, {tx, ty} = t) when abs(hx - tx) == 2 and abs(hy - ty) == 1,
    do: {(hx - tx) |> div(2), hy - ty} |> move_end(t)

  def move_tail({hx, hy}, {tx, ty} = t) when abs(hy - ty) == 2 and abs(hx - tx) == 1,
    do: {hx - tx, (hy - ty) |> div(2)} |> move_end(t)

  def move_tail({hx, hy}, {tx, ty} = t),
    do: {(hx - tx) |> div(2), (hy - ty) |> div(2)} |> move_end(t)

  def move(step, {{head_position, tail_position}, visited_positions}) do
    new_head_position = step |> move_end(head_position)
    new_tail_position = new_head_position |> move_tail(tail_position)

    {{new_head_position, new_tail_position}, visited_positions |> MapSet.put(new_tail_position)}
  end

  def calculate(input) do
    {_, visited_positions} =
      input |> parse |> Enum.reduce({{{0, 0}, {0, 0}}, MapSet.new()}, &move/2)

    visited_positions |> Enum.count()
  end
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule ExampleTest do
  use ExUnit.Case
  import Day9Part1

  assert example |> calculate == 13
end
```

Simulate your complete hypothetical series of motions. How many positions does the tail of the rope visit at least once?

```elixir
input = Kino.Input.textarea("Paste your puzzle input:")
```

```elixir
input |> Kino.Input.read() |> Day9Part1.calculate()
```
